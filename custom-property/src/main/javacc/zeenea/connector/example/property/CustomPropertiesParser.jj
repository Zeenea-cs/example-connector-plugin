options {
  STATIC = false;
  JDK_VERSION = "11";
  JAVA_TEMPLATE_TYPE = "modern";
  UNICODE_INPUT = true;
  IGNORE_CASE = true;
}

PARSER_BEGIN(CustomPropertiesParser)
package zeenea.connector.example.property;

import java.util.ArrayList;
import java.util.List;
import zeenea.connector.property.PropertyDefinition;
import zeenea.connector.property.PropertyType;

class CustomPropertiesParser {
  private String unquote(String text) {
    var sb = new StringBuilder();
    for (int i = 1, n = text.length() - 1; i < n; ++i) {
      char c = text.charAt(i);
      if (c == '\\' && i < n - 1) {
        sb.append(text.charAt(++i));
      } else {
        sb.append(c);
      }
    }
    return sb.toString();
  }
}
PARSER_END(CustomPropertiesParser)

SKIP :
{
  <WHITESPACE: [
     " ",      // regular space character
     "\t",     // tab character
     "\f",     // formfeed character
     "\u0085", // 'NEXT LINE (NEL)' (U+0085)
     "\u00A0", // 'NO-BREAK SPACE' (U+00A0)
     "\ufeff"  // 'ZERO WIDTH NO-BREAK SPACE' (U+FEFF)
  ]>
| <WINDOWS_EOL : "\r\n">
| <UNIX_EOL: "\n">
| <OLD_MAC_EOL: "\r">
| <COMMENT: "#" (~["\n","\r"])* >
}

/* Types */
TOKEN:
{
    <STRING: "string">
|   <LONG: "long">
|   <TEXT: "text">
|   <NUMBER: "number">
|   <URL: "url">
|   <INSTANT: "instant">
|   <TAG: "tag">
}

/* Keywords. */
TOKEN:
{
    <FROM: "from">
}

/* values */
TOKEN:
{
    <QUOTED_NAME: "'" (~["'"] | "\\'" )*  "'">
|   <RAW_NAME: <FIRST_LETTER> (<NEXT_LETTER>)*>
|   <#FIRST_LETTER: [ "_", "a"-"z", "A"-"Z" ]>
|   <#NEXT_LETTER: [ "-", "_", "a"-"z", "A"-"Z", "0"-"9" ]>
}

List<CustomProperty> propertyList():
{
    List<CustomProperty> properties = new ArrayList<>();
    CustomProperty property;
}
{
    ( property = property() { properties.add(property); } )*
    { return properties; }
}

CustomProperty property():
{
    PropertyType propertyType;
    String code;
    String attribute = null;
}
{
    (
        <STRING>         { propertyType = PropertyType.STRING; }
    |   <TAG>            { propertyType = PropertyType.TAG; }
    |   (<LONG>)? <TEXT> { propertyType = PropertyType.LONG_TEXT; }
    |   <NUMBER>         { propertyType = PropertyType.NUMBER; }
    |   <URL>            { propertyType = PropertyType.URL; }
    |   <INSTANT>        { propertyType = PropertyType.INSTANT; }
    )
    code = name()
    ( <FROM> attribute = name() )?
    { return CustomProperty.of(propertyType, code, attribute != null ? attribute : code); }
}

String name():
{
    Token nameToken;
    String name;
}
{
    (
        nameToken = <QUOTED_NAME> { name = unquote(nameToken.image); }
    |   nameToken = <RAW_NAME> { name = nameToken.image; }
    )
    { return name; }
}